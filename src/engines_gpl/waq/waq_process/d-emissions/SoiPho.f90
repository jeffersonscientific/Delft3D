      subroutine SOIPHO     ( pmsa   , fl     , ipoint , increm, noseg , &
                              noflux , iexpnt , iknmrk , noq1  , noq2  , &
                              noq3   , noq4   )
!DEC$ ATTRIBUTES DLLEXPORT, ALIAS: 'SOIPHO' :: SOIPHO
!*******************************************************************************
!
      IMPLICIT NONE
!
!     Type    Name         I/O Description
!
      real(4) pmsa(*)     !I/O Process Manager System Array, window of routine to process library
      real(4) fl(*)       ! O  Array of fluxes made by this process in mass/volume/time
      integer ipoint(*)  ! I  Array of pointers in pmsa to get and store the data
      integer increm(*)  ! I  Increments in ipoint for segment loop, 0=constant, 1=spatially varying
      integer noseg       ! I  Number of computational elements in the whole model schematisation
      integer noflux      ! I  Number of fluxes, increment in the fl array
      integer iexpnt(4,*) ! I  From, To, From-1 and To+1 segment numbers of the exchange surfaces
      integer iknmrk(*)   ! I  Active-Inactive, Surface-water-bottom, see manual for use
      integer noq1        ! I  Nr of exchanges in 1st direction (the horizontal dir if irregular mesh)
      integer noq2        ! I  Nr of exchanges in 2nd direction, noq1+noq2 gives hor. dir. reg. grid
      integer noq3        ! I  Nr of exchanges in 3rd direction, vertical direction, pos. downward
      integer noq4        ! I  Nr of exchanges in the bottom (bottom layers, specialist use only)
!
!*******************************************************************************
!     D-EM Preprocessor to initialize Nitrogen coefficients
!     

!
!     Type    Name         I/O Description                                        Unit
!
      integer            :: iseg

    ! PMSA admin 
      integer,parameter   :: lins = 5
      integer,parameter   :: louts = 2
      integer            :: ipnt(lins+louts)    !    Local work array for the pointering

      ! pointers to concrete items
      integer,parameter   :: ip_ptot  = 1
      integer,parameter   :: ip_plab  = 2
      integer,parameter   :: ip_thick = 3
      integer,parameter   :: ip_poros = 4
      integer,parameter   :: ip_dmden = 5
      integer,parameter   :: ip_qtot  = 6
      integer,parameter   :: ip_flab  = 7


      ! input and output items
      real :: ptot, plab
      real :: thick
      real :: poros
      real :: dmden
      real :: qtot, flab, qlab
      
      logical first
      data first /.true./
      save

      if (.not.first) return

      ! loop for processing
      ipnt = ipoint(1:lins+louts)

      do iseg = 1 , noseg
            
        ptot  = max(pmsa(ipnt(ip_ptot)),0.0)
        plab  = max(pmsa(ipnt(ip_plab)),0.0)
        thick = pmsa(ipnt(ip_thick))
        poros = min(pmsa(ipnt(ip_poros)),0.99)
        dmden = max(pmsa(ipnt(ip_dmden)),100.)
          
        ! mg/kg        g/m2      m              kg/m3
        qtot = 1000. * ptot / (thick*(1.-poros)*dmden)
        qlab = 1000. * plab / (thick*(1.-poros)*dmden)
        flab = max(min(qlab/qtot,1.0),0.0)
                 
        pmsa(ipnt(ip_qtot )) = qtot
        pmsa(ipnt(ip_flab )) = flab
          
        ipnt = ipnt + increm(1:lins+louts)
      enddo
      
      first = .false.
      return
      end
